INCLUDE_DIRECTORIES(
  ${BULLET_PHYSICS_SOURCE_DIR}/src
  ${BULLET_PHYSICS_SOURCE_DIR}/examples
  ${BULLET_PHYSICS_SOURCE_DIR}/examples/ThirdPartyLibs
  ${BULLET_PHYSICS_SOURCE_DIR}/examples/ThirdPartyLibs/enet/include
  ${BULLET_PHYSICS_SOURCE_DIR}/examples/ThirdPartyLibs/clsocket/src
  ${PYTHON_INCLUDE_DIRS}
)
IF(BUILD_PYBULLET_NUMPY)
  INCLUDE_DIRECTORIES(${PYTHON_NUMPY_INCLUDE_DIR})
ENDIF()

ADD_DEFINITIONS(-DSTATIC_LINK_SPD_PLUGIN)

SET(
  pybullet_SRCS
  pybullet.c
)

ADD_LIBRARY(pybullet SHARED ${pybullet_SRCS})

SET_TARGET_PROPERTIES(pybullet PROPERTIES PREFIX "")
SET_TARGET_PROPERTIES(pybullet PROPERTIES POSTFIX "")

SET_TARGET_PROPERTIES(pybullet PROPERTIES VERSION ${BULLET_VERSION})
SET_TARGET_PROPERTIES(pybullet PROPERTIES SOVERSION ${BULLET_VERSION})
SET_TARGET_PROPERTIES(pybullet PROPERTIES DEBUG_POSTFIX "_d")

IF(WIN32)
  IF(BUILD_PYBULLET_ENET OR BUILD_PYBULLET_CLSOCKET)
    TARGET_LINK_LIBRARIES(pybullet ws2_32)
  ENDIF(BUILD_PYBULLET_ENET OR BUILD_PYBULLET_CLSOCKET)

  SET_TARGET_PROPERTIES(pybullet PROPERTIES SUFFIX ".pyd")
ENDIF(WIN32)

IF(APPLE)
  SET_TARGET_PROPERTIES(pybullet PROPERTIES SUFFIX ".so")
ENDIF()

TARGET_LINK_LIBRARIES(
  pybullet
	bullet_c_api
)

IF(WIN32)
  TARGET_LINK_LIBRARIES(pybullet ${PYTHON_LIBRARIES})
ELSEIF(APPLE)
  SET_TARGET_PROPERTIES(
    pybullet
    PROPERTIES LINK_FLAGS "-undefined dynamic_lookup"
  )
ENDIF()
# else Linux: dont link

IF(WIN32)
  SET(PYTHON_SITE_PACKAGES Lib/site-packages CACHE PATH "Python install path")
ELSE()
  SET(
    PYTHON_SITE_PACKAGES
    lib/python${PYTHON_VERSION_MAJOR}.${PYTHON_VERSION_MINOR}/site-packages
    CACHE PATH "Python install path"
  )
ENDIF()

INSTALL(TARGETS pybullet DESTINATION ${PYTHON_SITE_PACKAGES})
